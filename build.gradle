apply plugin: 'java'

defaultTasks 'jar'

version = '0.3.1'
ext.vaadinVersion='7.1.9'

repositories {
    mavenCentral()
}

configurations {
    gwt
}

dependencies {
    compile group: 'com.vaadin', name: 'vaadin-client', version: vaadinVersion
    compile group: 'com.vaadin', name: 'vaadin-themes', version: vaadinVersion
    compile group: 'com.vaadin', name: 'vaadin-server', version: vaadinVersion
    gwt group: 'com.vaadin', name: 'vaadin-client-compiler', version: vaadinVersion
}

ext.widgetSetName = 'com.ejt.vaadin.loginform.WidgetSet'
ext.widgetSetBuildDir = mkdir(file("${buildDir}/widgetset"))

ext.compileClassPath = files(
        sourceSets.main.java.srcDirs,
        sourceSets.main.output.classesDir,
        configurations.compile.files,
        configurations.gwt.files
)

compileJava {
    sourceCompatibility = '1.6'
    targetCompatibility = '1.6'
}

clean {
    dependsOn 'cleanCompileWidgetSet'
}

task compileWidgetSet {

    dependsOn 'classes'

    inputs.files compileClassPath
    inputs.files sourceSets.main.java.srcDirs
    outputs.dir widgetSetBuildDir

    doLast {
        javaexec {
            maxHeapSize = '512m'
            jvmArgs = ['-Xss1024k', '-Djava.awt.headless=true']
            classpath = compileClassPath
            main = 'com.google.gwt.dev.Compiler'
            args = ['-war', widgetSetBuildDir, widgetSetName]
        }

        delete file("${widgetSetBuildDir}/WEB-INF")
    }
}


task devmode(type: JavaExec) {
    classpath = compileClassPath
    main = 'com.google.gwt.dev.DevMode'
    jvmArgs = ['-XX:MaxPermSize=400m', '-Xmx800m']
    args = ['-noserver', '-war', 'build/idea/artifacts/exploded_war/VAADIN/widgetsets', 'com.ejt.vaadin.loginform.WidgetSet', '-startupUrl', 'http://localhost:8080/test']
}

jar {
    baseName 'loginform'
    from sourceSets.main.java.srcDirs

    manifest {
        attributes(
            'Vaadin-Widgetsets': widgetSetName,
            'Implementation-Vendor': 'Ingo Kegel',
            'Implementation-Title': 'LoginForm',
            'Implementation-Version': version ,
            'Vaadin-Package-Version': '1',
            'Vaadin-License-Title': 'Apache License 2.0',
            'Vaadin-License-File': 'http://www.apache.org/licenses/LICENSE-2.0.html'
        )
    }
}
